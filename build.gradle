plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id 'maven-publish'
}

group = 'me.vekster'
version = '1.2.7'

repositories {
    mavenCentral()
    // Config Updater
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    // Floodgate
    maven { url 'https://repo.opencollab.dev/main/' }
    // FoliaLib
    maven {
        name = "tcoded-releases"
        url = "https://repo.tcoded.com/releases"
    }
}

dependencies {
    // JetBrains Annotations
    implementation 'org.jetbrains:annotations:26.0.1'
    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    // Spigot API
    compileOnly files('comp/spigot-1.8.8-R0.1-SNAPSHOT-latest.jar')
    // Netty
    compileOnly 'io.netty:netty-all:4.2.3.Final'
    // LightInjector
    implementation files('impl/LightInjector-1.0.2-forked.jar')
    // FoliaLib
    implementation 'com.tcoded:FoliaLib:0.5.1'
    // Floodgate
    compileOnly 'org.geysermc.floodgate:api:2.2.5-SNAPSHOT'
    // Config Updater
    implementation 'com.tchristofferson:ConfigUpdater:2.2'
    // MultiVersion
    implementation fileTree('impl/multiversion')
}

java {
    // Try to use a local JAVA_HOME for Java 8 if available. If not present, avoid automatic
    // toolchain provisioning (which may try to download JDKs) and fall back to source/target
    // compatibility for Java 8 so the build uses the system JVM.
    def javaHome = System.getenv('JAVA_HOME')
    if (javaHome) {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
            installationPath.set(file(javaHome))
        }
        // Also set compat for tools that read sourceCompatibility/targetCompatibility
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    } else {
        // No JAVA_HOME -> don't configure automatic toolchain provisioning (it would try to
        // download a JDK and fail if repositories aren't configured). Use the JVM running
        // Gradle and compile for Java 8 compatibility.
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        // Ensure JavaCompile uses -source/-target or --release where supported
        tasks.withType(JavaCompile).configureEach {
            // options.release is available on newer Gradle/JDKs; guard to avoid errors on older toolchains
            try {
                options.release.set(8)
            } catch (Exception ignored) {
                options.compilerArgs += ['-source', '1.8', '-target', '1.8']
            }
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/tiredvekster/LightAntiCheat"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }


    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['plugin.yml']) {
        expand props
    }
}

shadowJar {
    exclude('LICENSE', 'META-INF/**')
    relocate('com.fren_gor', 'org.fren_gor')
    relocate('com.tchristofferson', 'org.tchristofferson')
    relocate('com.tcoded', 'org.tcoded')
    archiveClassifier.set('')
    destinationDirectory.set(file(destinationPath))
}

// Ensure the shadow (fat) JAR is produced during the regular build so dependencies
// like ConfigUpdater are included in the plugin JAR that you deploy to the server.
tasks.named('build') {
    dependsOn shadowJar
}
